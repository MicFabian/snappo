plugins {
  id 'groovy'
  id 'java-library'
  id 'maven-publish'
  id 'signing'
  id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
}

group = 'io.github.micfabian'
version = '1.0.4'

def groovyVersion = '5.0.4'

def spockVersion = '2.4-groovy-5.0'

def slf4jVersion = '2.0.13'

def junitPlatformVersion = '1.10.2'

def logbackVersion = '1.5.11'
def isReleaseVersion = !version.toString().endsWith('-SNAPSHOT')
def prop = { String key, String fallback = '' ->
  (findProperty(key) ?: fallback).toString()
}

repositories {
  mavenCentral()
}

dependencies {
  api platform("org.apache.groovy:groovy-bom:${groovyVersion}")
  api 'org.apache.groovy:groovy'
  api 'org.apache.groovy:groovy-json'
  api 'org.apache.groovy:groovy-xml'

  api "org.slf4j:slf4j-api:${slf4jVersion}"

  compileOnly "org.spockframework:spock-core:${spockVersion}"

  testImplementation "org.spockframework:spock-core:${spockVersion}"
  testRuntimeOnly "org.junit.platform:junit-platform-launcher:${junitPlatformVersion}"
  testRuntimeOnly "ch.qos.logback:logback-classic:${logbackVersion}"
}

test {
  useJUnitPlatform()
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
  withSourcesJar()
  withJavadocJar()
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
      pom {
        name = prop('POM_NAME', 'snappo')
        description = prop('POM_DESCRIPTION', 'Spock snapshot testing helpers for Groovy/Java projects')
        url = prop('POM_URL', 'https://github.com/MicFabian/snappo')

        licenses {
          license {
            name = prop('POM_LICENSE_NAME', 'The Apache License, Version 2.0')
            url = prop('POM_LICENSE_URL', 'https://www.apache.org/licenses/LICENSE-2.0.txt')
            distribution = 'repo'
          }
        }

        developers {
          developer {
            id = prop('POM_DEVELOPER_ID', 'micfabian')
            name = prop('POM_DEVELOPER_NAME', 'Michael Fabian')
            email = prop('POM_DEVELOPER_EMAIL', 'mvirks@web.de')
          }
        }

        scm {
          connection = prop('POM_SCM_CONNECTION', 'scm:git:https://github.com/MicFabian/snappo.git')
          developerConnection = prop('POM_SCM_DEV_CONNECTION', 'scm:git:ssh://git@github.com:MicFabian/snappo.git')
          url = prop('POM_SCM_URL', 'https://github.com/MicFabian/snappo')
        }
      }
    }
  }
}

signing {
  required { isReleaseVersion }
  def signingKey = findProperty('signingKey')?.toString()
  def signingKeyId = findProperty('signingKeyId')?.toString()
  def signingPassword = findProperty('signingPassword')?.toString()
  if (signingKey) {
    // Gradle properties often store armored keys with literal "\n".
    def normalizedSigningKey = signingKey.replace('\\n', '\n')
    useInMemoryPgpKeys(signingKeyId, normalizedSigningKey, signingPassword)
  } else {
    useGpgCmd()
  }
  sign publishing.publications
}

nexusPublishing {
  repositories {
    sonatype {
      nexusUrl = uri('https://ossrh-staging-api.central.sonatype.com/service/local/')
      snapshotRepositoryUrl = uri('https://central.sonatype.com/repository/maven-snapshots/')
      username = prop('sonatypeUsername')
      password = prop('sonatypePassword')
    }
  }
}

tasks.register('publishToMavenCentral') {
  group = 'publishing'
  description = 'Publishes this project to Maven Central via Sonatype Central Portal'
  if (isReleaseVersion) {
    dependsOn 'publishToSonatype', 'closeAndReleaseSonatypeStagingRepository'
  } else {
    dependsOn 'publishToSonatype'
  }
}
